!$Revision: 1.1 $
!$Date: 2007/06/05 15:13:52 $
!$RCSfile: generate_water.inp,v $
! this is file: $C/python/Refine/explicit_solvent/generate_water.inp

! generate_water.inp
! soaks a protein structure in a layer of water
! can be applied iteratively (using dyncount > 1)
!
!     ************************************
!     * Authors and copyright:           *
!     * Michael Nilges, Jens Linge, EMBL *
!     * No warranty implied or expressed *
!     * All rights reserved              *
!     ************************************
!     MODIFIED FOR USE WITH XPLOR-NIH, CHRIS SPRONK

eval ($boxlength = 18.856)   ! length of Brooks' water box
eval ($thickness = 8)        ! maxi. initial water-protein distance (heavy atoms)
eval ($pw_dist = 4.0)        ! mini. initial water-protein distance (heavy atoms)
eval ($water_diam = 2.4)     ! diameter of water molecule
eval ($dyncount = 1)         ! iteration number (usually 1)

eval ($water = "WAT" + encode($dyncount))

!--------------------------------------------------
! read in the same box of water several times, and move it around
! so as to cover all the space around the site of interest.
! take into account box offset

vector show max (x) ((not resn tip3) and not resn ani)
evaluate ($xmax = $result)
vector show min (x) ((not resn tip3) and not resn ani)
evaluate ($xmin = $result)

vector show max (y) ((not resn tip3) and not resn ani)
evaluate ($ymax = $result)
vector show min (y) ((not resn tip3) and not resn ani)
evaluate ($ymin = $result)

vector show max (z) ((not resn tip3) and not resn ani)
evaluate ($zmax = $result)
vector show min (z) ((not resn tip3) and not resn ani)
evaluate ($zmin = $result)


! loop over several iterations of water filling and dynamics


!--------------------------------------------------
! read in the same box of water several times, and move it around
! so as to cover all the space around the site of interest.
! take into account box offset


! determine how many boxes are necessary in each dimension
eval ($xbox = int( ($xmax - $xmin + 2 * ($thickness + $water_diam)) / $boxlength  + 0.5))
eval ($ybox = int( ($ymax - $ymin + 2 * ($thickness + $water_diam)) / $boxlength  + 0.5))
eval ($zbox = int( ($zmax - $zmin + 2 * ($thickness + $water_diam)) / $boxlength  + 0.5))

eval ($xmtran =  $xmax + $thickness - $boxlength/2 + $water_diam)
eval ($ymtran =  $ymax + $thickness - $boxlength/2 + $water_diam)
eval ($zmtran =  $zmax + $thickness - $boxlength/2 + $water_diam)

set echo off message off end
eval ($xcount=0)
eval ($xtrans = $xmin - $thickness - $water_diam - $boxlength/2 )
while ($xtrans < $xmtran) loop wat1
  eval ($xcount=$xcount+1)
  eval ($xtrans = $xtrans + $boxlength)

  eval ($ycount=0)
  eval ($ytrans = $ymin - $thickness - $water_diam - $boxlength/2 )
  while ($ytrans < $ymtran) loop wat2
    eval ($ycount=$ycount+1)
    eval ($ytrans = $ytrans + $boxlength)

    eval ($zcount=0)
    eval ($ztrans = $zmin - $thickness - $water_diam - $boxlength/2 )
    while ($ztrans < $zmtran) loop wat3
      eval ($zcount=$zcount+1)
      eval ($ztrans = $ztrans + $boxlength)


      segment
        name="    "
        chain
          coordinates @@SOLVENT:boxtyp20.pdb
        end
      end
      coor @@SOLVENT:boxtyp20.pdb
      vector do (segid=W000) (segid "    ")
      coor sele=(segid W000) translate vector = ($xtrans $ytrans $ztrans) end

      ! all new water oxygens
      vector identity (store1) (segid W000 and name oh2)
      ! all new water oxygens close to a protein heavy atom
      vector identity (store2) (store1 and (not (resn tip3 or resn ani or hydro)) around $pw_dist)
      ! all new water oxygens close to old water oxygens
      vector identity (store3) (store1 and (segid wat# and not hydro) around $water_diam)
      ! all new water oxygens further than thickness away from a protein heavy atom
      vector identity (store4) (store1 and not (not (resn tip3 or resn ani or hydro)) around $thickness)
      delete sele= (byres (store2 or store3 or store4)) end


      ! give waters unique segid name
      eval ($segid= "W"
             + encode($xcount) + encode($ycount) + encode($zcount))
      vector do (segid = $segid) (segid W000)

    end loop wat3
  end loop wat2
end loop wat1

! now, give waters a unique resid so that we get the segid to play around with
vector identity (store1) (all)
vector show min (store1) (segid w*)
vector do (store1 = store1 - $result + 1) (segid w*)
vector do (resid = encode(int(store1/3 -0.1) +1)) (segid w* and not segid wat#)
vector do (segid = $water) (segid w* and not segid wat#)

! shave off any waters that left
delete sele= (byres (name oh2 and not (not (resn tip3 or resn ani or hydro)) around $thickness)) end

{* write out initial coordinates *}
!   evaluate ($filename=$Filenames.fileroot+ encode($count)+ "wini.pdb")
!   write coordinates sele= (all) output =$filename end

set echo on message on end
